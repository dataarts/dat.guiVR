<!DOCTYPE html>
<html>
  <head>
    <title>VR Dat GUI Shiny Example</title>
    <meta charset="UTF-8">
    <meta content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=0, minimal-ui" name="viewport" />
    <meta name="mobile-web-app-capable" content="yes">
    <meta name="apple-mobile-web-app-capable" content="yes">
    <link rel="stylesheet" type="text/css" href="style.css">
  </head>
  <body>

    <script type="text/javascript" src="https://cdn.rawgit.com/mrdoob/three.js/r83/build/three.js"></script>
    <script type="text/javascript" src="https://cdn.rawgit.com/mflux/three-vr-viewer/2.2.0/three-vr-viewer.js"></script>

    <script type="text/javascript" src="../build/datguivr.js"></script>

    <script type="text/javascript" src="js/floorandlights.js"></script>

    <script type="text/javascript" src="thirdparty/noisesim.js"></script>
    <script type="text/javascript" src="thirdparty/noiseparticle.js"></script>
    <script type="text/javascript" src="thirdparty/noiseshadow.js"></script>

    <!--
      Shaders by @thespite
      https://github.com/spite/polygon-shredder/blob/master/index.html
    -->

    <script id="texture_vertex_simulation_shader" type="x-shader/x-vertex">
      varying vec2 vUv;
      varying vec3 vOffset;
      uniform vec3 offset;
      uniform mat4 inverseModelViewMatrix;
      void main() {
        vOffset = ( inverseModelViewMatrix * vec4( offset, 1. ) ).xyz;
        vUv = vec2(uv.x, 1.0 - uv.y);
        gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
      }
    </script>

    <script id="texture_fragment_simulation_shader" type="x-shader/x-fragment">
    //
    // Description : Array and textureless GLSL 2D/3D/4D simplex
    //               noise functions.
    //      Author : Ian McEwan, Ashima Arts.
    //  Maintainer : ijm
    //     Lastmod : 20110822 (ijm)
    //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
    //               Distributed under the MIT License. See LICENSE file.
    //               https://github.com/ashima/webgl-noise
    //
    vec3 mod289(vec3 x) {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    vec4 mod289(vec4 x) {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }
    vec4 permute(vec4 x) {
      return mod289(((x*34.0)+1.0)*x);
    }
    vec4 taylorInvSqrt(vec4 r){
      return 1.79284291400159 - 0.85373472095314 * r;
    }
    float snoise(vec3 v) {
      const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
      const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
      // First corner
      vec3 i  = floor(v + dot(v, C.yyy) );
      vec3 x0 =   v - i + dot(i, C.xxx) ;
      // Other corners
      vec3 g = step(x0.yzx, x0.xyz);
      vec3 l = 1.0 - g;
      vec3 i1 = min( g.xyz, l.zxy );
      vec3 i2 = max( g.xyz, l.zxy );
      //   x0 = x0 - 0.0 + 0.0 * C.xxx;
      //   x1 = x0 - i1  + 1.0 * C.xxx;
      //   x2 = x0 - i2  + 2.0 * C.xxx;
      //   x3 = x0 - 1.0 + 3.0 * C.xxx;
      vec3 x1 = x0 - i1 + C.xxx;
      vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
      vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y
      // Permutations
      i = mod289(i);
      vec4 p = permute( permute( permute(
          i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
        + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
        + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
      // Gradients: 7x7 points over a square, mapped onto an octahedron.
      // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
      float n_ = 0.142857142857; // 1.0/7.0
      vec3  ns = n_ * D.wyz - D.xzx;
      vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)
      vec4 x_ = floor(j * ns.z);
      vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)
      vec4 x = x_ *ns.x + ns.yyyy;
      vec4 y = y_ *ns.x + ns.yyyy;
      vec4 h = 1.0 - abs(x) - abs(y);
      vec4 b0 = vec4( x.xy, y.xy );
      vec4 b1 = vec4( x.zw, y.zw );
      //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
      //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
      vec4 s0 = floor(b0)*2.0 + 1.0;
      vec4 s1 = floor(b1)*2.0 + 1.0;
      vec4 sh = -step(h, vec4(0.0));
      vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
      vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
      vec3 p0 = vec3(a0.xy,h.x);
      vec3 p1 = vec3(a0.zw,h.y);
      vec3 p2 = vec3(a1.xy,h.z);
      vec3 p3 = vec3(a1.zw,h.w);
      //Normalise gradients
      vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;
      // Mix final noise value
      vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
      m = m * m;
      return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3) ) );
    }
    vec3 snoiseVec3( vec3 x ){
      float s  = snoise(vec3( x ));
      float s1 = snoise(vec3( x.y - 19.1 , x.z + 33.4 , x.x + 47.2 ));
      float s2 = snoise(vec3( x.z + 74.2 , x.x - 124.5 , x.y + 99.4 ));
      vec3 c = vec3( s , s1 , s2 );
      return c;
    }
    vec3 curlNoise( vec3 p ){
      const float e = .1;
      vec3 dx = vec3( e   , 0.0 , 0.0 );
      vec3 dy = vec3( 0.0 , e   , 0.0 );
      vec3 dz = vec3( 0.0 , 0.0 , e   );
      vec3 p_x0 = snoiseVec3( p - dx );
      vec3 p_x1 = snoiseVec3( p + dx );
      vec3 p_y0 = snoiseVec3( p - dy );
      vec3 p_y1 = snoiseVec3( p + dy );
      vec3 p_z0 = snoiseVec3( p - dz );
      vec3 p_z1 = snoiseVec3( p + dz );
      float x = p_y1.z - p_y0.z - p_z1.y + p_z0.y;
      float y = p_z1.x - p_z0.x - p_x1.z + p_x0.z;
      float z = p_x1.y - p_x0.y - p_y1.x + p_y0.x;
      const float divisor = 1.0 / ( 2.0 * e );
      return normalize( vec3( x , y , z ) * divisor );
    }
    vec3 curlNoise2( vec3 p ) {
      const float e = .1;
      vec3 xNoisePotentialDerivatives = snoiseVec3( p );
      vec3 yNoisePotentialDerivatives = snoiseVec3( p + e * vec3( 3., -3.,  1. ) );
      vec3 zNoisePotentialDerivatives = snoiseVec3( p + e * vec3( 2.,  4., -3. ) );
      vec3 noiseVelocity = vec3(
        zNoisePotentialDerivatives.y - yNoisePotentialDerivatives.z,
        xNoisePotentialDerivatives.z - zNoisePotentialDerivatives.x,
        yNoisePotentialDerivatives.x - xNoisePotentialDerivatives.y
      );
      return normalize( noiseVelocity );
    }
    vec4 snoiseD(vec3 v) { //returns vec4(value, dx, dy, dz)
      const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
      const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
      vec3 i  = floor(v + dot(v, C.yyy) );
      vec3 x0 =   v - i + dot(i, C.xxx) ;
      vec3 g = step(x0.yzx, x0.xyz);
      vec3 l = 1.0 - g;
      vec3 i1 = min( g.xyz, l.zxy );
      vec3 i2 = max( g.xyz, l.zxy );
      vec3 x1 = x0 - i1 + C.xxx;
      vec3 x2 = x0 - i2 + C.yyy;
      vec3 x3 = x0 - D.yyy;
      i = mod289(i);
      vec4 p = permute( permute( permute(
                 i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
               + i.y + vec4(0.0, i1.y, i2.y, 1.0 ))
               + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
      float n_ = 0.142857142857; // 1.0/7.0
      vec3  ns = n_ * D.wyz - D.xzx;
      vec4 j = p - 49.0 * floor(p * ns.z * ns.z);
      vec4 x_ = floor(j * ns.z);
      vec4 y_ = floor(j - 7.0 * x_ );
      vec4 x = x_ *ns.x + ns.yyyy;
      vec4 y = y_ *ns.x + ns.yyyy;
      vec4 h = 1.0 - abs(x) - abs(y);
      vec4 b0 = vec4( x.xy, y.xy );
      vec4 b1 = vec4( x.zw, y.zw );
      vec4 s0 = floor(b0)*2.0 + 1.0;
      vec4 s1 = floor(b1)*2.0 + 1.0;
      vec4 sh = -step(h, vec4(0.0));
      vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
      vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
      vec3 p0 = vec3(a0.xy,h.x);
      vec3 p1 = vec3(a0.zw,h.y);
      vec3 p2 = vec3(a1.xy,h.z);
      vec3 p3 = vec3(a1.zw,h.w);
      vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;
      vec4 values = vec4( dot(p0,x0), dot(p1,x1), dot(p2,x2), dot(p3,x3) ); //value of contributions from each corner (extrapolate the gradient)
      vec4 m = max(0.5 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0); //kernel function from each corner
      vec4 m2 = m * m;
      vec4 m3 = m * m * m;
      vec4 temp = -6.0 * m2 * values;
      float dx = temp[0] * x0.x + temp[1] * x1.x + temp[2] * x2.x + temp[3] * x3.x + m3[0] * p0.x + m3[1] * p1.x + m3[2] * p2.x + m3[3] * p3.x;
      float dy = temp[0] * x0.y + temp[1] * x1.y + temp[2] * x2.y + temp[3] * x3.y + m3[0] * p0.y + m3[1] * p1.y + m3[2] * p2.y + m3[3] * p3.y;
      float dz = temp[0] * x0.z + temp[1] * x1.z + temp[2] * x2.z + temp[3] * x3.z + m3[0] * p0.z + m3[1] * p1.z + m3[2] * p2.z + m3[3] * p3.z;
      return vec4(dot(m3, values), dx, dy, dz) * 42.0;
    }
    vec3 curlNoise3 (vec3 p) {
        vec3 xNoisePotentialDerivatives = snoiseD( p ).yzw; //yzw are the xyz derivatives
        vec3 yNoisePotentialDerivatives = snoiseD(vec3( p.y - 19.1 , p.z + 33.4 , p.x + 47.2 )).zwy;
        vec3 zNoisePotentialDerivatives = snoiseD(vec3( p.z + 74.2 , p.x - 124.5 , p.y + 99.4 )).wyz;
        vec3 noiseVelocity = vec3(
            zNoisePotentialDerivatives.y - yNoisePotentialDerivatives.z,
            xNoisePotentialDerivatives.z - zNoisePotentialDerivatives.x,
            yNoisePotentialDerivatives.x - xNoisePotentialDerivatives.y
        );
      const float e = .1;
      const float divisor = 1.0 / ( 2.0 * e );
      return normalize( noiseVelocity * divisor );
    }
    varying vec2 vUv;
    varying vec3 vOffset;
    uniform float active;
    uniform sampler2D tPositions;
    uniform sampler2D oPositions;
    uniform float width;
    uniform float height;
    uniform vec3 offset;
    uniform float timer;
    uniform float delta;
    uniform float speed;
    uniform float reset;
    uniform float genScale;
    uniform float factor;
    uniform float evolution;
    uniform float radius;
    mat4 rotationMatrix(vec3 axis, float angle) {
        axis = normalize(axis);
        float s = sin(angle);
        float c = cos(angle);
        float oc = 1.0 - c;
        return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
                    oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
                    oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
                    0.0,                                0.0,                                0.0,                                1.0);
    }
    float rand(vec2 co){
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }
    void main() {
      vec4 c = texture2D( tPositions, vUv );
      vec3 pos = c.xyz;
      float life = c.a;
      float s = vUv.x * life / 100.;
      float speedInc = 1.;
      if( s > .95 ) speedInc = .75;
      else if( s > .9 ) speedInc = .85;
      else speedInc = 1.;
      //speedInc *= 2. * ( 1. - ( life / 100. ) );
      //speedInc *= .25;
      vec3 v = factor * speedInc * delta * speed * ( curlNoise( .2 * pos + factor * evolution * .1 * timer ) );
      //vec3 v = speedInc * delta * speed * ( snoiseVec3( .2 * pos + 5. * timer ) );
      pos += v;
      life -= factor * 1.;
      if( length( pos ) < radius ) {
        pos = normalize( pos ) * radius;
      }
      if( life <= 0. || reset == 1. ) {
        pos = ( rotationMatrix( vec3( 1., 0., 0. ), timer ) * texture2D( oPositions, vUv ) ).xyz + vOffset;
        //pos = genScale * texture2D( oPositions, vUv ).xyz + vOffset;
        life = 100.;
      }
      gl_FragColor = vec4( pos, life );
    }
    </script>

    <script id="vs-particles" type="x-shader/x-vertex">
    precision highp float;
    attribute vec3 position;
    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;
    uniform mat3 normalMatrix;
    uniform mat4 modelMatrix;
    uniform mat4 viewMatrix;
    uniform vec3 cameraPosition;
    uniform sampler2D map;
    uniform sampler2D prevMap;
    uniform vec3 boxVertices[ 36 ];
    uniform vec3 boxNormals[ 3 ];
    uniform float width;
    uniform float height;
    uniform float timer;
    uniform vec3 boxScale;
    uniform float meshScale;
    varying vec3 vPosition;
    varying vec4 vColor;
    varying vec4 vShadowCoord;
    uniform mat4 shadowV;
    uniform mat4 shadowP;
    uniform vec3 lightPosition;
    varying vec3 vLightPosition;
    uniform sampler2D diffuse;
    const mat4 biasMatrix = mat4(
      0.5, 0.0, 0.0, 0.0,
      0.0, 0.5, 0.0, 0.0,
      0.0, 0.0, 0.5, 0.0,
      0.5, 0.5, 0.5, 1.0
    );
    mat4 rotationMatrix(vec3 axis, float angle) {
      axis = normalize(axis);
      float s = sin(angle);
      float c = cos(angle);
      float oc = 1.0 - c;
      return mat4(oc * axis.x * axis.x + c,           oc * axis.x * axis.y - axis.z * s,  oc * axis.z * axis.x + axis.y * s,  0.0,
            oc * axis.x * axis.y + axis.z * s,  oc * axis.y * axis.y + c,           oc * axis.y * axis.z - axis.x * s,  0.0,
            oc * axis.z * axis.x - axis.y * s,  oc * axis.y * axis.z + axis.x * s,  oc * axis.z * axis.z + c,           0.0,
            0.0,                                0.0,                                0.0,                                1.0);
    }
    float ramp( float x ) {
      return 1. - 1. - pow( 1. - x, 4. );
    }
    float parabola( float x, float k ) {
      return pow( 4. * x * ( 1. - x ), k );
    }
    float random(vec4 seed4){
      //return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
      float dot_product = dot(seed4, vec4(12.9898,78.233,45.164,94.673));
      return fract(sin(dot_product) * 43758.5453);
    }
    mat3 calcLookAtMatrix(vec3 origin, vec3 target, float roll) {
      vec3 rr = vec3(sin(roll), cos(roll), 0.0);
      vec3 ww = normalize(target - origin);
      vec3 uu = normalize(cross(ww, rr));
      vec3 vv = normalize(cross(uu, ww));
      return mat3(uu, vv, ww);
    }
    void main() {
      vec2 dimensions = vec2( width, height );
      float px = position.y;
      float vi = position.z;
      float x = mod( px, dimensions.x );
      float y = mod( floor( px / dimensions.x ), dimensions.y );
      vec2 uv = vec2( x, y ) / dimensions;
      vec4 cubePosition = texture2D( map, uv );
      vec4 prevPosition = texture2D( prevMap, uv );
      float alpha = cubePosition.a / 100.;
      float scale = .025 * parabola( 1. - alpha, 1. );
      vec3 faceNormal = boxNormals[ int( vi / 6. ) ];
      mat4 localRotation = mat4( calcLookAtMatrix( cubePosition.xyz, prevPosition.xyz, 0. ) );
      vec4 rotatedNormal = localRotation * vec4( faceNormal, 1. );
      vec3 visPosition = ( modelMatrix * ( cubePosition + rotatedNormal * scale ) ).xyz;
      float d = dot( normalize( visPosition - cameraPosition ), normalize( ( modelMatrix * rotatedNormal ).xyz ) );
      vec3 boxVertex = boxVertices[ int( vi + ( 1. - step( 0., d ) ) * 18. ) ];
      vec3 modifiedVertex = ( ( localRotation * vec4( boxVertex * scale * boxScale * meshScale, 1. ) ).xyz );
      vec3 modifiedPosition = cubePosition.xyz + modifiedVertex;
      gl_Position = projectionMatrix * modelViewMatrix * vec4( modifiedPosition, 1.0 );
      vPosition = modifiedPosition;
      vShadowCoord = biasMatrix * shadowP * shadowV * modelMatrix * vec4( modifiedPosition, 1. );
      vColor = texture2D( diffuse, uv );
      vLightPosition = lightPosition;
    }
    </script>

    <script id="fs-particles" type="x-shader/x-fragment">
    #extension GL_OES_standard_derivatives : enable
    precision highp float;
    uniform float spread;
    varying vec3 vPosition;
    varying vec4 vColor;
    varying vec4 vShadowCoord;
    uniform sampler2D depthTexture;
    uniform sampler2D projector;
    varying vec3 vLightPosition;
    uniform vec2 resolution;
    uniform vec3 cameraPosition;
    float bias;
    float unpackDepth( const in vec4 rgba_depth ) {
      const vec4 bit_shift = vec4(1.0/(256.0*256.0*256.0), 1.0/(256.0*256.0), 1.0/256.0, 1.0);
        return dot(rgba_depth, bit_shift);
    }
    float random(vec4 seed4){
        //return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
        float dot_product = dot(seed4, vec4(12.9898,78.233,45.164,94.673));
      return fract(sin(dot_product) * 43758.5453);
    }
    float sampleVisibility( vec3 coord ) {
      return step( coord.z, unpackDepth( texture2D( depthTexture, coord.xy + 0. * ( .5 - random( vec4( coord, bias ) ) ) / 2048. ) ) + bias );
    }
    mat2 rotationMatrix( float a ) {
      return mat2( cos( a ), sin( a ),
              -sin( a ), cos( a ) );
    }
    const float PI = 3.14159265358979323846264;
    void main() {
    /*  vec3 fdx = vec3(dFdx(vPosition.x),dFdx(vPosition.y),dFdx(vPosition.z));
      vec3 fdy = vec3(dFdy(vPosition.x),dFdy(vPosition.y),dFdy(vPosition.z));
      vec3 n = normalize(cross(fdx,fdy));*/
      vec3 fdx = dFdx( vPosition );
      vec3 fdy = dFdy( vPosition );
      vec3 n = normalize(cross(fdx, fdy));
      vec4 base = vec4( 1. );
      vec3 L = normalize( vLightPosition - vPosition );
      vec3 E = normalize( cameraPosition - vPosition );
      float diffuse = max( 0., dot( L, n ) );
      float theta = clamp( -diffuse, 0., 1. );
        bias = 0.005 * tan( acos( theta ) );
        bias = clamp( bias, 0., 0.01 );
      float shadow = 0.;
      vec3 shadowCoord = vShadowCoord.xyz / vShadowCoord.w;
      float step = spread;
      vec2 inc = vec2( step ) / resolution;
      shadow += sampleVisibility( shadowCoord + vec3(     0., -inc.y, 0. ) );
      shadow += sampleVisibility( shadowCoord + vec3( -inc.x,     0., 0. ) );
      shadow += sampleVisibility( shadowCoord + vec3(     0.,     0., 0. ) );
      shadow += sampleVisibility( shadowCoord + vec3(  inc.x,     0., 0. ) );
      shadow += sampleVisibility( shadowCoord + vec3(     0.,  inc.y, 0. ) );
      shadow /= 5.;
      vec4 mask = texture2D( projector, vShadowCoord.xy );
      float shininess = 200.;
      vec3 halfVector = normalize(E + L );
      float specular = dot(n, halfVector);
      specular = max(0.0, specular);
      specular = pow(specular, shininess);
      float ambient = .2;
      float o = diffuse * shadow * mask.r;
      vec3 color = mix( vColor.rgb, vec3( 1. ), .8 * clamp( -n.y, 0., 1. ) );
      vec3 diffuseColor = color * mix( vec3( o ), vec3( 1. ), ambient );
      vec3 specularColor = vec3( 1. );
      base.rgb = mix( diffuseColor, specularColor, specular * o );
      gl_FragColor = base;
    }
    </script>

    <script id="fs-particles-shadow" type="x-shader/x-fragment">
    precision highp float;
    vec4 packDepth(const in float depth) {
      const vec4 bit_shift = vec4(256.0*256.0*256.0, 256.0*256.0, 256.0, 1.0);
      const vec4 bit_mask  = vec4(0.0, 1.0/256.0, 1.0/256.0, 1.0/256.0);
      vec4 res = mod(depth*bit_shift*vec4(255), vec4(256))/vec4(255);
      res -= res.xxyz * bit_mask;
      return res;
    }
    void main() {
      gl_FragColor = packDepth( gl_FragCoord.z );
    }
    </script>


    <script type="text/javascript">


      const { scene, camera, events, toggleVR, controllers, renderer } = VRViewer({
        emptyRoom: false,
        THREE
      });

      //  you can use the mouse to interact with the GUI on-screen!
      dat.GUIVR.enableMouse( camera, renderer );

      //  or turn it off when going into VR
      window.addEventListener( 'vrdisplaypresentchange', function( e ){
        if( e.display.isPresenting ){
          dat.GUIVR.disableMouse();
        }
        else{
          dat.GUIVR.enableMouse( camera, renderer );
        }
      }, false );

      const guiInputRight = dat.GUIVR.addInputObject( controllers[ 0 ] );
      scene.add( guiInputRight );

      const guiInputLeft = dat.GUIVR.addInputObject( controllers[ 1 ] );
      scene.add( guiInputLeft );


      const params = {
        type: 2,
        spread: 4,
        factor: .5,
        evolution: .5,
        rotation: .5,
        radius: 0.2,
        pulsate: false,
        scaleX: .1,
        scaleY: 3,
        scaleZ: 15,
        scale: 0.25
      };



      const gui = dat.GUIVR.create( 'Settings' );
      gui.position.y = 1.5;
      scene.add( gui );

      gui.add( params, 'spread', 0, 10 ).step( 0.01 );
      gui.add( params, 'factor', 0, 1 ).step( 0.01 );
      gui.add( params, 'evolution', 0, 1 ).step( 0.01 );
      gui.add( params, 'rotation', 0, 1 ).step( 0.01 );
      gui.add( params, 'radius', 0, 1 ).step( 0.01 );
      gui.add( params, 'pulsate' );
      gui.add( params, 'scaleX', 0, 20 ).step( 0.1 );
      gui.add( params, 'scaleY', 0, 20 ).step( 0.1 );
      gui.add( params, 'scaleZ', 0, 20 ).step( 0.1 );
      gui.add( params, 'scale', 0, 2 ).step( 0.01 );







      const simSize = 256;
      const shadowSize = 1024;

      const sim = createNoiseSim( renderer, simSize, params );

      const { shadowCamera, shadowBuffer } = createShadow( scene, renderer, shadowSize );
      const shadowMaterial = createShadowMaterial( sim.rtTexturePos, shadowSize );

      const geo = createGeometry( simSize, 1024 );
      const mat = createMaterial( simSize, shadowSize, sim.rtTexturePos, shadowBuffer );
      const mesh = new THREE.Mesh( geo, mat );
      mesh.scale.set( 0.2, 0.2, 0.2 );
      scene.add( mesh );

      // const shadowDebug = new THREE.Mesh( new THREE.PlaneGeometry( 10,10 ), new THREE.MeshBasicMaterial( { map: shadowBuffer, side: THREE.DoubleSide } ) );
      // scene.add( shadowDebug );

      const clock = new THREE.Clock();
      const m = new THREE.Matrix4();
      const nOffset = new THREE.Vector3();
      const tmpVector = new THREE.Vector3();
      const v = new THREE.Vector3();
      let scale = 0, nScale = 0.2;

      events.on('tick', function updateLoop( dt ){

        scale += ( nScale - scale ) * .01;

        const delta = clock.getDelta() * 10;
        const time = clock.elapsedTime;


        //  Simulation
        tmpVector.copy( nOffset );
        tmpVector.sub( sim.simulationShader.uniforms.offset.value );
        tmpVector.multiplyScalar( .1 );
        sim.simulationShader.uniforms.offset.value.add( tmpVector );
        sim.simulationShader.uniforms.factor.value = params.factor;
        sim.simulationShader.uniforms.evolution.value = params.evolution;
        sim.simulationShader.uniforms.radius.value = params.radius;
        if( sim.simulationShader.uniforms.active.value ) {
          mesh.rotation.y = params.rotation * time;
        }
        m.copy( mesh.matrixWorld );
        sim.simulationShader.uniforms.inverseModelViewMatrix.value.getInverse( m );
        sim.simulationShader.uniforms.genScale.value = 0.1;

        if( sim.simulationShader.uniforms.active.value === 1 ) {
          sim.render( time, delta );
        }



        //  Shadow render
        mat.uniforms.map.value = shadowMaterial.uniforms.map.value = sim.targets[ sim.targetPos ];
        mat.uniforms.prevMap.value = shadowMaterial.uniforms.prevMap.value = sim.targets[ 1 - sim.targetPos ];

        mat.uniforms.spread.value = params.spread;
        mat.uniforms.timer.value = shadowMaterial.uniforms.timer.value = time;
        mat.uniforms.boxScale.value.set( params.scaleX, params.scaleY, params.scaleZ );
        shadowMaterial.uniforms.boxScale.value.set( params.scaleX, params.scaleY, params.scaleZ );
        mat.uniforms.meshScale.value = params.scale;
        shadowMaterial.uniforms.meshScale.value = params.scale;

        renderer.setClearColor( 0 );
        mesh.material = shadowMaterial;
        renderer.render( scene, shadowCamera, shadowBuffer );

        tmpVector.copy( scene.position );
        tmpVector.sub( shadowCamera.position );
        tmpVector.normalize();

        m.makeRotationY( -mesh.rotation.y );
        v.copy( shadowCamera.position );
        v.applyMatrix4( m );

        mat.uniforms.shadowP.value.copy( shadowCamera.projectionMatrix );
        mat.uniforms.shadowV.value.copy( shadowCamera.matrixWorldInverse );
        mat.uniforms.lightPosition.value.copy( v );

        renderer.setClearColor( 0x202020 );
        mesh.material = mat;


      });

    </script>

  </body>
</html>